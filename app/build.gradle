import org.jlleitschuh.gradle.ktlint.reporter.ReporterType

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jlleitschuh.gradle.ktlint' version '11.0.0'
}

def getCurrentBranch() {
    def branch = ""
    def proc = "git name-rev --name-only HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

String branchName = getCurrentBranch()


android {
    namespace 'com.autumnsun.jenkinsbase'
    compileSdk 34

    defaultConfig {
        applicationId "com.autumnsun.jenkinsbase"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BRANCH_NAME", "\"" + branchName + "\""
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def formattedDate = new Date().format("yyyyMMddHHmmss")
            output.outputFileName = "app-${variant.name}-${formattedDate}.apk"
        }
    }

    buildFeatures {
        buildConfig true
    }

}

ktlint {
    android = true
    outputColorName = "RED"
    ignoreFailures = true
    reporters {
        reporter(ReporterType.HTML)
        reporter(ReporterType.CHECKSTYLE)
        reporter(ReporterType.JSON)
    }
}


dependencies {

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}